// Top-level build file where you can add configuration options common to all sub-projects/modules.
ext {
    vCompileSdkVersion = 21
    vBuildToolsVersion = '23.0.1'
    vMinSdkVersion = 14
    vTargetSdkVersion = 21

    vGlobalVersionCode = 11
    vGlobalVersionName = '2.3.3'

    vVersionCode = vGlobalVersionCode
    vVersionName = vGlobalVersionName

    //initialized from local.properties
    vBintrayUser = null
    vBintrayKey = null

    loadProperties()
}

buildscript {

    repositories {
        jcenter()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:1.3.0'
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:0.6'
        classpath 'com.github.dcendents:android-maven-plugin:1.2'

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

allprojects {

    repositories {
        jcenter()
    }

}

final publishedProjects = ['root', 'literepo', 'literepo-conversion']
configure(allprojects.findAll {publishedProjects.contains(it.name)}) {

    evaluationDependsOn(':' + project.name)

    apply plugin: 'com.jfrog.bintray'

    group = 'ingvar.android.literepo'
    version = project.vVersionName

    bintray {
        user = vBintrayUser
        key = vBintrayKey
        configurations = ['archives']
        publish = true

        pkg {
            repo = 'maven'
            name = 'literepo'
            desc = 'Simplify your work with sqlite in android.\nCore library: contains builders and content provider.\nConversion library: contains converters and annotations.'
            websiteUrl = 'https://github.com/orwir/literepo'
            issueTrackerUrl = 'https://github.com/orwir/literepo/issues'
            vcsUrl = 'https://github.com/orwir/literepo.git'
            licenses = ['Apache-2.0']
            labels = ['sqlite', 'android']
            publicDownloadNumbers = true
        }
    }

}

//-------------------------------------------------
//HELPFUL FUNCTIONS

void loadProperties() {
    File local = file('local.properties')
    if(local.exists()) {
        Properties props = new Properties()
        props.load(new FileInputStream(local))

        ext.vBintrayUser = props['bintray.user']
        ext.vBintrayKey = props['bintray.key']
    }
    if(!ext.vBintrayUser?.trim()) {//get from environment
        ext.vBintrayUser = System.getenv('bintray_user')
        ext.vBintrayKey = System.getenv('bintray_key')
    }
}
